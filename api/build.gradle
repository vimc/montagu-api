group 'org.vaccineimpact'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

task run(dependsOn: ':app:run')
task generateDatabaseInterface(dependsOn: ':generateDatabaseInterface:run')

def loadPropertiesFile(path) {
    def props = new Properties()
    def f = file(path)
    if (f.exists()) {
        f.withReader {
            props.load(it)
        }
    }
    return props
}

def loadProperties() {
    def properties = loadPropertiesFile("config/default.properties")
    def userFile = file("config/current_user")
    if (userFile.exists()) {
        def user = userFile.text.trim()
        print("Using configuration for user $user")
        def userPropertiesFile = file("config/users/${user}.properties")
        if (userPropertiesFile.exists()) {
            def userProperties = loadPropertiesFile(userPropertiesFile.path)
            properties.putAll(userProperties)
        } else {
            throw new Exception("You defined a user file, with user '$user', " +
                    "but no corresponding properties file exists at $userPropertiesFile.absolutePath")
        }
    }
    return properties
}

subprojects {
    Properties properties
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        jcenter()
    }

    task loadPropertiesTask() {
        properties = loadProperties()
    }

    task copyConfig(type: Copy, dependsOn: 'loadPropertiesTask') {
        from 'src/main/resources'
        include '*'
        into 'build/classes/main/'
        expand(properties)
    }

    copyConfig.dependsOn = ['loadPropertiesTask']
    processResources.finalizedBy copyConfig
}