buildscript {
    repositories {
        // This is needed for gradle-docker
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'application'
apply plugin: 'docker'

mainClassName = "org.vaccineimpact.api.security.AppKt"

dependencies {
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "org.simplejavamail:simple-java-mail:4.4.5"
    compile project(":security")

    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile project(":testHelpers")
}

def cli_docker_version = 'UNKNOWN'
if (hasProperty('docker_version')) {
    cli_docker_version = docker_version
}
def cli_docker_name = 'montagu-cli'
if (hasProperty('docker_name')) {
    cli_docker_name = docker_name
}

distDocker {
    baseImage = "libsodium"
    dependsOn = [build]
    push = true
    tag = cli_docker_name
    tagVersion = cli_docker_version
}

test.dependsOn ':startDatabase'
test.finalizedBy ':stopDatabase'
// We run the tests as a suite (see UserCLITestSuite)
// This line prevents them being run individually as well
test.exclude "**/org/vaccineimpact/api/security/tests/**"
